// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'


        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    compileSdkVersion = 27
    buildToolsVersion = "27.0.3"

    supportLibVersion = "27.1.1"
    gmsLibVersion = "15.0.1"
    minSdkVersion = 14
    targetSdkVersion = 27
}

configure(subprojects.findAll { it.name != 'hogefuga' }) {
    evaluationDependsOn(":${project.name}")
    apply plugin: 'findbugs'
    task findbugs(type: FindBugs) {
        ignoreFailures = true
        effort = "default"
        reportLevel = "medium"
        excludeFilter = new File("$project.rootDir/findbugs-android-exclude")
        jvmArgs = ['-Duser.language=en']
        classes = files("build/intermediates/classes/")
        source 'src/main'
        include '**/*.java'
        reports {
            xml.enabled = true
            xml.withMessages = true
            html.enabled = !xml.isEnabled()
            if (xml.enabled) {
                xml.setDestination(new File("$project.buildDir/reports/findbugs_report.xml"))
            } else {
                html.setDestination(new File("$project.buildDir/reports/findbugs_report.html"))
            }
        }
        classpath = files()
    }
    check.dependsOn 'findbugs'
}

